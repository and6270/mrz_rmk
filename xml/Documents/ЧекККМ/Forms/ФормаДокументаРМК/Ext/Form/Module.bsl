
&НаСервере
&Вместо("ОбновитьЗаголовокФормы")
Процедура Расш66_ОбновитьЗаголовокФормы()
	Заголовок = НСтр("ru = 'Продажа (ИП: %Организация%, Продавец: %Продавец%)'");
	Заголовок = СтрЗаменить(Заголовок, "%Организация%", Объект.Организация);
	Заголовок = СтрЗаменить(Заголовок, "%Продавец%", ?(ЗначениеЗаполнено(ТекущийПродавец),ТекущийПродавец, НСтр("ru='<Не выбран>'")));
	
	ЦветЗаголовка = Справочники.КассыККМ.Расш66_ПолучитьЦвет(Объект.КассаККМ);
	Если ЦветЗаголовка <> Неопределено Тогда
		Элементы.ГруппаВерхняяКоманднаяПанель.ЦветФона = ЦветЗаголовка;
	Иначе 
		Элементы.ГруппаВерхняяКоманднаяПанель.ЦветФона = Элементы.ГруппаВерхняяКоманднаяПанельРасширеннаяПодсказка.ЦветФона;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш66_ОтложитьПосле(Команда)
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЧековККМ.Пробит") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Ссылка", Неопределено);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьНовыйЧекЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РезультатОтложенДо = КонецДня(Объект.Дата + 24*60*60);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РезервироватьДо", РезультатОтложенДо);
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
		
	Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемОперацияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры = Новый Структура("ОповещениеПриЗавершении", Оповещение);
	
	//вызов стандартной процедуры конфигурации для откладывания чека с резервированием 
	ЗарезервироватьНаКлиенте(РезультатОтложенДо, ДополнительныеПараметры)
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПробитьЧекПослеПроведения")
Процедура Расш66_ПробитьЧекПослеПроведения(Проведен, ПараметрыОперацииФискализацииЧека)

	Если НЕ Проведен Тогда
		Возврат;
	КонецЕсли;

	ОписаниеОповещения = Новый ОписаниеОповещения("ПробитьЧекЗавершение", ЭтотОбъект);
    #Вставка
	Если ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования И ТипЗнч(ПараметрыКассыККМ) = Тип("ФиксированнаяСтруктура")Тогда
		ПодправитьПараметрыКассыККМ(ПараметрыКассыККМ);
	КонецЕсли;
	#КонецВставки
	Результат = МенеджерОборудованияУТКлиент.ОборудованиеПодключено(ПараметрыКассыККМ.ИдентификаторУстройства);

	Если Результат Или ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования Тогда

		Если Не ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования Тогда

			ПроверитьКодМаркировкиСредствамиККТ(ПараметрыОперацииФискализацииЧека);

		Иначе

			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);

			РезультатВыполнения = Новый Структура;
			РезультатВыполнения.Вставить("Результат", Истина);
			ПечатьЧека_Завершение(
			РезультатВыполнения,
			ДополнительныеПараметры);

		КонецЕсли;

	Иначе

		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Фискальное устройство не подключено. Чек не напечатан.'"));

		ВыполнитьОбработкуОповещения(
		ОписаниеОповещения,
		Новый Структура("ВыполненаОперацияНаУстройстве, ИзмененныеДанныеЗаписаны",
		Ложь, Ложь));

	КонецЕсли;

КонецПроцедуры 

&НаСервереБезКонтекста
Процедура ПодправитьПараметрыКассыККМ(ПараметрыКассыККМ)
	// Вычислим запросом нужное подключаемое оборудование
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыКассыККМ.НастройкиРМК);
	Запрос.УстановитьПараметр("Организация", ПараметрыКассыККМ.Организация);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	НастройкиРМККассыККМ.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ИЗ
	|	Справочник.НастройкиРМК.КассыККМ КАК НастройкиРМККассыККМ
	|ГДЕ
	|	НастройкиРМККассыККМ.Ссылка = &Ссылка
	|	И НастройкиРМККассыККМ.КассаККМ.Владелец = &Организация
	|	И НЕ НастройкиРМККассыККМ.ИспользоватьБезПодключенияОборудования";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		// подменим параметры в структуре
		НоваяСтруктура = Новый Структура;
		Для Каждого Элемент Из ПараметрыКассыККМ Цикл
			Если Элемент.Ключ = "ИспользоватьБезПодключенияОборудования" Тогда 
				НоваяСтруктура.Вставить(Элемент.Ключ, Ложь);
			ИначеЕсли Элемент.Ключ = "ИдентификаторУстройства" Тогда
				НоваяСтруктура.Вставить(Элемент.Ключ, Результат.ПодключаемоеОборудование);
			Иначе
				НоваяСтруктура.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		НовыйФиксСтруктура  = Новый ФиксированнаяСтруктура(НоваяСтруктура);
		ПараметрыКассыККМ = НовыйФиксСтруктура;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Расш66_ОтключитьЭквайринговыйТерминалПосле(Команда)
	Для Каждого Терминал Из ЭквайринговыеТерминалы Цикл
		Терминал.Значение.ИспользоватьБезПодключенияОборудования = Истина;
	КонецЦикла;
КонецПроцедуры

