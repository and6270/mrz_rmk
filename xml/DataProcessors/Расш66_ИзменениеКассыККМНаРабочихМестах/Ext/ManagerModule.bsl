Процедура УстановитьКассуККМПоУмолчанию(КассаККМ, МассивРабочиеМеста) Экспорт
	ККМ = Справочники.КассыККМ.НайтиПоНаименованию(КассаККМ,Истина);
	Для Каждого РабочееМесто Из МассивРабочиеМеста Цикл
		ОбъектРабочееМесто = РабочееМесто.ПолучитьОбъект();
		Для Каждого СтрокаКассыККТ Из ОбъектРабочееМесто.КассыККМ Цикл
			Если СтрокаКассыККТ.ИспользоватьБезПодключенияОборудования И СтрокаКассыККТ.КассаККМ.Склад = ККМ.Склад Тогда
				СтрокаКассыККТ.КассаККМ = ККМ;				
			КонецЕсли;
		КонецЦикла;
		Попытка
			ОбъектРабочееМесто.Записать();
		Исключение
			Сообщить("ОШИБКА: Ошибка настройки рабочего места: " + ОписаниеОшибки());	
		КонецПопытки;	
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТекущиеКассыККМПоСкладам() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(КассыККМ.Ссылка) КАК КоличествоКасс,
		|	КассыККМ.Склад КАК Склад
		|ПОМЕСТИТЬ ВсеКассыККМ
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	НЕ КассыККМ.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	КассыККМ.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеКассыККМ.Склад КАК Склад
		|ПОМЕСТИТЬ ВТ_КассыБольшеОдной
		|ИЗ
		|	ВсеКассыККМ КАК ВсеКассыККМ
		|ГДЕ
		|	ВсеКассыККМ.КоличествоКасс > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиРМККассыККМ.Ссылка КАК Ссылка,
		|	НастройкиРМККассыККМ.КассаККМ КАК КассаККМ,
		|	1 КАК Счетчик
		|ПОМЕСТИТЬ ВТ_РабочиеМеста
		|ИЗ
		|	Справочник.НастройкиРМК.КассыККМ КАК НастройкиРМККассыККМ
		|ГДЕ
		|	НастройкиРМККассыККМ.ИспользоватьБезПодключенияОборудования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КассыККМ.Склад КАК Склад,
		|	КассыККМ.Ссылка КАК Касса,
		|	ЕСТЬNULL(ВТ_РабочиеМеста.Ссылка, НЕОПРЕДЕЛЕНО) КАК РабочееМесто,
		|	ЕСТЬNULL(ВТ_РабочиеМеста.Счетчик, 0) КАК Счетчик
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РабочиеМеста КАК ВТ_РабочиеМеста
		|		ПО КассыККМ.Ссылка = ВТ_РабочиеМеста.КассаККМ
		|ГДЕ
		|	КассыККМ.Склад В
		|			(ВЫБРАТЬ
		|				ВТ_КассыБольшеОдной.Склад КАК Склад
		|			ИЗ
		|				ВТ_КассыБольшеОдной КАК ВТ_КассыБольшеОдной)
		|УПОРЯДОЧИТЬ ПО
		|	Счетчик УБЫВ
		|ИТОГИ
		|	СУММА(Счетчик)
		|ПО
		|	Склад,
		|	Касса";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	МассивРезультат = Новый Массив;
	Пока ВыборкаСклад.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСклад
		Если ВыборкаСклад.Счетчик = 0 Тогда
			Продолжить;	
		КонецЕсли;
		СтруктураСклада = Новый Структура;
		СтруктураСклада.Вставить("Склад", ВыборкаСклад.Склад);
		МассивРабочиеМеста = Новый Массив;
		МассивКассыККМ = Новый Массив;
		ВсегоККМ = ВыборкаСклад.Счетчик;
		
		ВыборкаКасса = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаКасса.Следующий() Цикл
			// Вставить обработку выборки ВыборкаКасса
	        МассивКассыККМ.Добавить(Новый Структура("Касса,Счетчик", ВыборкаКасса.Касса, ВыборкаКасса.Счетчик));
			ВыборкаДетальныеЗаписи = ВыборкаКасса.Выбрать();
	
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если ВыборкаДетальныеЗаписи.РабочееМесто <> Неопределено Тогда
					МассивРабочиеМеста.Добавить(ВыборкаДетальныеЗаписи.РабочееМесто);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		СтруктураСклада.Вставить("РабочиеМеста", МассивРабочиеМеста);
		СтруктураСклада.Вставить("КассыККМ", МассивКассыККМ);
		МассивРезультат.Добавить(СтруктураСклада);
	КонецЦикла;                            
	Возврат МассивРезультат;
КонецФункции
